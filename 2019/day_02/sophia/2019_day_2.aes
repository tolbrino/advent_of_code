include "List.aes"
include "Pair.aes"

contract AdventOfCode2019Day02 =

  entrypoint solve_part1() : int =
    let input' = prepare(input(), 12, 2)
    result_1(solve_part1'(0, input'))

  entrypoint solve_part2() : int =
    let range = List.from_to(0, 99)
    let pairs = List.map((n) => [(n, n') | n' <- range], range)
    solve_part2'(List.flatten(pairs))

  function result_1(program : list(int)) : int =
    switch(List.first(program))
      None =>
        0
      Some(n) =>
        n

  function result_2(noun : int, verb : int) : int =
    noun * 100 + verb

  function solve_part1'(pos : int, program : list(int)) : list(int) =
    let op = List.get(pos, program)
    switch(op)
      99 =>
        program
      1 =>
         let a = List.get(pos + 1, program)
         let b = List.get(pos + 2, program)
         let dest = List.get(pos + 3, program)
         let value = List.get(a, program) + List.get(b, program)
         let program1 = List.replace_at(dest, value, program)
         solve_part1'(pos + 4, program1)
      2 =>
         let a = List.get(pos + 1, program)
         let b = List.get(pos + 2, program)
         let dest = List.get(pos + 3, program)
         let value = List.get(a, program) * List.get(b, program)
         let program1 = List.replace_at(dest, value, program)
         solve_part1'(pos + 4, program1)
      n =>
        abort(String.concat("Invalid op instruction: ", Int.to_str(n)))

  function solve_part2'(pairs : list((int * int))) : int =
    let pairs' = List.drop_while(check_pair, pairs)
    switch(List.first(pairs'))
      None =>
        abort("No solution for problem 2 found")
      Some((a, b)) =>
        result_2(a, b)

  function check_pair(pair : int * int) : bool =
    let input = prepare(input(), Pair.fst(pair), Pair.snd(pair))
    let program = solve_part1'(0, input)
    result_1(program) != 19690720

  function prepare(program : list(int), noun : int, verb : int) : list(int) =
      let program1 = List.replace_at(1, noun, program)
      let program2 = List.replace_at(2, verb, program1)
      program2

  function input() : list(int) =
      [1,0,0,3,1,1,2,3,1,3,4,3,1,5,0,3,2,13,1,19,1,19,10,23,1,23,6,27,1,6,27,31,1,13,31,35,1,13,35,39,1,39,13,43,2,43,9,47,2,6,47,51,1,51,9,55,1,55,9,59,1,59,6,63,1,9,63,67,2,67,10,71,2,71,13,75,1,10,75,79,2,10,79,83,1,83,6,87,2,87,10,91,1,91,6,95,1,95,13,99,1,99,13,103,2,103,9,107,2,107,10,111,1,5,111,115,2,115,9,119,1,5,119,123,1,123,9,127,1,127,2,131,1,5,131,0,99,2,0,14,0]
